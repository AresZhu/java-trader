

buildscript {
    ext {
        springVersion = '5.1.8.RELEASE'
        springBootVersion = '2.1.6.RELEASE'
        jettyVersion = '9.4.18.v20190429'
    }
    repositories {
        mavenLocal()
        mavenCentral()
        jcenter()
    }
    dependencies {
        classpath(
        "org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
    }
}

subprojects {
    apply plugin: "eclipse"
    apply plugin: 'java'
    apply plugin: "maven"

    group = 'trader'
    version = '1.0.0'
    ext {
        jtraderCommonVersion = '1.0.0.0-20190528'
        jctpLinuxVersion = '6.3.15_20190220-linux_x64' //'6.3.11-20180109-linux_x64', '6.3.13_20181119-linux_x64', '6.3.15_20190220-linux_x64'

        gsonVersion = '2.8.5'
        guavaVersion = '27.1-jre'
        juntVersion = '4.12'
        mysqlVersion = '8.0.8-dmr'
        ta4jVersion = '0.12'
        commonsTextVersion = '1.4'
        commonsCliVersion = '1.4'
        commonsVFSVersion = '2.2'
        jsoupVersion = '1.12.1'
        httpcomponentVersion = '4.5.9'
        jdom2Verson = '2.0.6'
        slf4jVersion = '1.7.26'
        fastUtilVersion = '8.2.2'
        rocksdbVersion = '5.17.2'
        reflectionsVersion = '0.9.11'
        disruptorVersion = '3.4.2'
        groovyVersion = '2.5.7'
    }
    ext.gitRevision = "git rev-parse --short HEAD".execute().text.trim()

    sourceCompatibility = 1.8
    targetCompatibility = 1.8

    tasks.withType(JavaCompile) {
        options.encoding = "UTF-8"
    }

    task sourceJar(type: Jar) {
        from sourceSets.main.allJava
    }

    jar {
        manifest {
            attributes(
                "Implementation-Title": project.group+"."+archivesBaseName,
                "Implementation-Version": project.version,
                'Built-Time': new Date().format("yyyy-MM-dd'T'HH:mm:ssZ"),
                'Git-Revision': gitRevision
                )
        }
    }

    repositories {
        mavenLocal()
        mavenCentral()
        jcenter()
    }

    //Export all classpath entries
    eclipse {
        classpath {
            downloadSources=true
            file {
                withXml {
                    xml -> xml.asNode().classpathentry.findAll{
                        it.@kind == 'lib' || it.@kind == 'src'
                    }.each{
                        it.@exported = 'true'
                    }
                }
            }
        }
    }
}

ext{
    traderHome = System.getenv('TRADER_HOME')
}

task localDeploy(type: Copy) {
    from('trader-services/build/libs') {
        include '*.jar'
    }
    from('trader-services/src/distribution') {
        include '*.sh'
    }
    into traderHome+'/apps/trader'
}